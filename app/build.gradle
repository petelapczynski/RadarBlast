apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'
    signingConfigs {
        config {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('keystore.properties').newDataInputStream())
            storeFile file(properties.getProperty('storeFile'))
            keyAlias properties.getProperty('keyAlias')
            storePassword properties.getProperty('storePassword')
            keyPassword properties.getProperty('keyPassword')
        }

    }
    defaultConfig {
        applicationId "petelap.radarblast"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 4
        versionName "BetaRelease"
        resValue 'string', "about_version_Summary", "Build 2019–02–21"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.config
        flavorDimensions "default"
    }
    buildTypes {
        release {
            signingConfig signingConfigs.config
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
            buildConfigField 'String', "appID", App_release_appID
            buildConfigField 'String', "unitID", App_release_unitID
            resValue 'string', "resappID", App_release_appID
        }
        debug {
            signingConfig signingConfigs.config
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            buildConfigField 'String', "appID", App_debug_appID
            buildConfigField 'String', "unitID", App_debug_unitID
            resValue 'string', "resappID", App_debug_appID
        }
    }
    productFlavors {
        free {
            applicationId "petelap.radarblast.free"
            manifestPlaceholders = [ appLabel: "Radar Blast" ]
        }
        paid {
            applicationId "petelap.radarblast.paid"
            manifestPlaceholders = [ appLabel: "Radar Blast" ]
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.google.android.gms:play-services-ads:17.1.3'
    implementation 'com.android.support:support-annotations:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}